buildscript {
    ext {
        kotlinVersion = '1.1.3'
    }

    repositories {
        google()
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

// a plugin to download files. Used for downloaded redex releases from github
plugins {
    id "de.undercouch.download" version "3.3.0"
}

apply plugin: 'idea'
apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'

group = "au.com.timmutton"
version = "1.4.0"

dependencies {
    repositories {
        google()
        mavenCentral()
        jcenter()
    }

    compile gradleApi()
    compile 'com.android.tools.build:gradle:2.3.3'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    compile 'com.google.code.gson:gson:2.8.0'
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.2'
    distributionUrl = "https://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}

task sourcesJar(type: Jar) {
    description = 'An archive of the source code for Maven Central'
    from sourceSets.main.kotlin
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc
}

// releases/latest is a webpage for the most recent release
// of redex. Search through the html for the name of the
// tag of this release.
import de.undercouch.gradle.tasks.download.Download
import java.util.regex.Pattern
import java.util.regex.Matcher
import java.nio.file.Files
def getLatestRedexTag = { ->
    def latest = "https://github.com/facebook/redex/releases/latest"
    download {
        src latest
        dest buildDir
        onlyIfNewer true
    }
    def input = new File("$buildDir/latest").text
    def pattern = Pattern.compile(
        '/facebook/redex/releases/tag/(v\\d+\\.\\d+\\.\\d+)')
    def matcher = pattern.matcher(input)
    matcher.find()
    return matcher.group(1)
}

// download redex from the github release page
import org.apache.tools.ant.taskdefs.condition.Os
task downloadRedex {
    if (Os.isFamily(Os.FAMILY_UNIX)) {
        def tag = getLatestRedexTag()
        def redex_exec = 'redex_linux_x86_64'
        def linux = "https://github.com/facebook/redex/releases/download/$tag/$redex_exec"
        download {
            src linux
            dest buildDir
            onlyIfNewer true
        }
        exec {
            commandLine 'chmod', 'u+x', "$buildDir/$redex_exec"
        }
    } else {
        println("Your platform isn't supported (yet) for downloading redex." +
                "Please follow instructions at" +
                "https://github.com/facebok/redex")
    }
}

artifacts {
    archives javadocJar, sourcesJar
}

// Bintray
Properties properties = new Properties()
File properties_file = project.rootProject.file('local.properties')
if (properties_file.exists()) {
  properties.load(properties_file.newDataInputStream())
}

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']

    pkg {
        repo = 'maven'
        name = 'redex'
        desc = 'A gradle plugin that allows you to use the Redex tool as part of your build process'
        websiteUrl = 'https://github.com/timmutton/redex-plugin/'
        vcsUrl = 'https://github.com/outware/redex-plugin.git'
        licenses = ["MIT"]
        publicDownloadNumbers = true
        publish = true
    }
}

bintrayUpload.dependsOn(install)
install.dependsOn(build)
